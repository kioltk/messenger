apply plugin: 'com.android.application'


def versionPropsFile = file('version.properties')
def currentVersionCode = 0
def currentVersionName = "0.0"
if (versionPropsFile.canRead()) {
    def Properties versionProps = new Properties()

    versionProps.load(new FileInputStream(versionPropsFile))

    currentVersionCode = versionProps['VERSION_CODE'].toInteger() + 1
    currentVersionName = versionProps['VERSION_NAME']
    versionProps['VERSION_CODE'] = currentVersionCode.toString()
    versionProps.store(versionPropsFile.newWriter(), null)
}

if (project.hasProperty('buildIndex')) {
    project.version = project.buildIndex + ""
} else {
    project.version = 'debug build' + currentVersionCode
}

project.archivesBaseName = "messenger";
android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.happysanta.messenger"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode currentVersionCode
        versionName project.version
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // support
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.google.android.gms:play-services:6.5.87'

    // utils
    compile project(':vksdk_library')
    compile project(':pickers')
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    compile 'com.eowise:recyclerview-stickyheaders:0.5.0@aar'

    // trackers
    compile 'com.splunk.mint:mint:4.0.8'

}
